apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'libmpv for Android'
                    url = 'https://github.com/jarnedemeulemeester/libmpv-android'
                    licenses {
                        license {
                            name = 'MIT license'
                            url = 'https://github.com/jarnedemeulemeester/libmpv-android/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'jarnedemeulemeester'
                            name = 'Jarne Demeulemeester'
                            email = 'jarnedemeulemeester@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com:jarnedemeulemeester/libmpv-android.git'
                        developerConnection = 'scm:git@github.com:jarnedemeulemeester/libmpv-android.git'
                        url = 'https://github.com/jarnedemeulemeester/libmpv-android/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
        rootProject.ext["signing.keyId"],
        rootProject.ext["signing.key"],
        rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
